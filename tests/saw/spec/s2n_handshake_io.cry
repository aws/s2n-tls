// Low-level specifications (model functions) of parts of tls/s2n_handshake_io.c.
// The main function is s2n_advance_message, and we prove it's equivalence to its
// counterpart in s2n_handshake_io.c

module s2n_handshake_io where

// This function models the update of the s2n_connection struct by the
// s2n_conn_set_handshake_type function in s2n.
conn_set_handshake_type : connection -> connection
conn_set_handshake_type conn = conn'
  where conn' = {handshake = handshake'
                ,mode = conn.mode
                ,corked_io = conn.corked_io
                ,corked = conn.corked
                ,is_caching_enabled = conn.is_caching_enabled
                ,resume_from_cache = conn.resume_from_cache
                ,server_can_send_ocsp = conn.server_can_send_ocsp
                ,key_exchange_eph = conn.key_exchange_eph
                ,client_auth_flag = conn.client_auth_flag}
        (handshake' : handshake) = {handshake_type = handshake_type'
                                   ,message_number = conn.handshake.message_number
                                   }
        handshake_type' = NEGOTIATED || full_handshake ||
                          (if (full_handshake != 0) then
                            perfect_forward_secrecy || ocsp_status || client_auth
                            else zero)
        full_handshake  = if (conn.is_caching_enabled && ~conn.resume_from_cache)
                          then zero
                          else FULL_HANDSHAKE
        perfect_forward_secrecy = if conn.key_exchange_eph
                                  then PERFECT_FORWARD_SECRECY
                                  else zero
        ocsp_status = if conn.server_can_send_ocsp
                      then OCSP_STATUS
                      else zero
        client_auth = if conn.client_auth_flag
                      then CLIENT_AUTH
                      else zero
                                                     
// This function models the update of the s2n_connection struct by the
// s2n_advance_message function in s2n.
advance_message : connection -> connection
advance_message conn = conn2
  where conn2 = {handshake = handshake2
                ,mode = conn.mode
                ,corked_io = conn.corked_io
                ,corked = cork2
                ,is_caching_enabled = conn.is_caching_enabled
                ,resume_from_cache = conn.resume_from_cache
                ,server_can_send_ocsp = conn.server_can_send_ocsp
                ,key_exchange_eph = conn.key_exchange_eph
                ,client_auth_flag = conn.client_auth_flag
                }
        (handshake2 : handshake) = {handshake_type = conn.handshake.handshake_type
                                   ,message_number = conn.handshake.message_number + 1
                                   }
        cork2 = if ((ACTIVE_STATE conn2).writer != (PREVIOUS_STATE conn2).writer)
                then if (ACTIVE_STATE conn2).writer == mode_writer conn2.mode
                     then s2n_cork conn else s2n_uncork conn
                else conn.corked

// for a given initial connection struct generate the
// sequence of handshake_action's
traceS2N : {n} (fin n) => connection -> [n]handshake_action
traceS2N conn = normalizeAfterData (take (map ACTIVE_STATE (iterate s2nTrans conn)))

traceS2N2 : {n} (fin n) => connection -> [n][32]
traceS2N2 conn = take (map ACTIVE_MESSAGE (iterate s2nTrans conn))

//Make sure all the messages after the first APPLICATION_DATA are also APPLICATION_DATA
normalizeAfterData : {n} (fin n) => [n]handshake_action -> [n]handshake_action
normalizeAfterData hsas = map snd (tail (scanl f (False, dataMsg) hsas))
    where f (found, acc) hsa = if hsa == dataMsg \/ found then (True, dataMsg)
                               else (found, hsa)
          dataMsg = mkAct TLS_APPLICATION_DATA 0 'B'

// returns the first element of an array that satisfies a given
// predicate. If none found, a provided default value is returned.
find : {n, x} (fin n) => (x -> Bool) -> x -> [n]x -> x
find p def xs = snd (foldl f (False, def) xs)
  where f (found, y) next = if ~found /\ p next then (True, next) else (found, y)

head : {n, x} [n+1]x -> x
head (([a] : [1]x) # _) = a

lookupDefault : {a, b, n} (fin n, Cmp a) => [n](a, b) -> b -> a -> b
lookupDefault dict def key = fst (foldl f (def, False) dict)
  where f (value, found) (key', value') =
          if found then (value, found)
          else if key == key' then (value', True)
                else (value, found)

fst (a, b) = a
snd (a, b) = b

// s2n state transition that incorporates both s2n_advance_message and
// s2n_conn_set_handshake_type
s2nTrans : connection -> connection
s2nTrans conn = conn''
          where conn' = if helloState then conn_set_handshake_type conn else conn
                conn'' = advance_message conn'
                helloState = ACTIVE_MESSAGE conn == CLIENT_HELLO \/ ACTIVE_MESSAGE conn == SERVER_HELLO

// Part of the s2n_connection struct that s2n_advance_message
// uses. The names of the fields are the same as in C.
type connection = {handshake : handshake
                  ,mode : [32]
                  ,corked_io : [8]
                  ,corked : [2]
                  ,is_caching_enabled : Bit
                  ,resume_from_cache : Bit
                  ,server_can_send_ocsp : Bit
                  ,key_exchange_eph : Bit
                  ,client_auth_flag : Bit //whether the server will request client cert
                  }

type handshake = {handshake_type : [32]
                 ,message_number : [32]
                 }

// {ACTIVE,PREVIOUS}_{STATE,MESSAGE} functions model the corresponding macros in C
ACTIVE_MESSAGE : connection -> [32]
ACTIVE_MESSAGE conn = (handshakes @ (conn.handshake.handshake_type))
                                  @ (conn.handshake.message_number)

PREVIOUS_MESSAGE : connection -> [32]
PREVIOUS_MESSAGE conn = (handshakes @ (conn.handshake.handshake_type))
                                    @ (conn.handshake.message_number - 1)

ACTIVE_STATE : connection -> handshake_action
ACTIVE_STATE conn = state_machine @ (ACTIVE_MESSAGE conn)

PREVIOUS_STATE : connection -> handshake_action
PREVIOUS_STATE conn = state_machine @ (PREVIOUS_MESSAGE conn)

// Helper function that gives the value of the 'writer' field of the
// current hanshake state (handshake action) that would correspond to
// the connection mode  (S2N_SERVER or S2N_CLIENT)
mode_writer : [32] -> [8]
mode_writer m = if m == S2N_CLIENT then 'C' else 'S'

s2n_cork : connection -> [2]
s2n_cork c = c.corked + 1

s2n_uncork : connection -> [2]
s2n_uncork c = c.corked - 1



// this models the handshake_action struct (part of the handshake
// state), but without the handler functions
type handshake_action = {record_type : [8]
                        ,message_type : [8]
                        ,writer : [8]
                        }

mkAct : [8] -> [8] -> [8] -> handshake_action
mkAct r m w = {record_type = r, message_type = m, writer = w}

// A model of the handshake states (array state_machine in C)
state_machine : [16]handshake_action
state_machine = [state_machine_fn m | m <- [0..15]]

// Function that tells a handshake_action for a given state
state_machine_fn : [4] -> handshake_action
state_machine_fn m =
  if      m == CLIENT_HELLO then mkAct TLS_HANDSHAKE TLS_CLIENT_HELLO 'C'
  else if m == SERVER_HELLO then mkAct TLS_HANDSHAKE TLS_SERVER_HELLO 'S'
  else if m == SERVER_NEW_SESSION_TICKET then mkAct TLS_HANDSHAKE TLS_SERVER_NEW_SESSION_TICKET 'S'
  else if m == SERVER_CERT then mkAct TLS_HANDSHAKE TLS_SERVER_CERT 'S'
  else if m == SERVER_CERT_STATUS then mkAct TLS_HANDSHAKE TLS_SERVER_CERT_STATUS 'S'
  else if m == SERVER_KEY then mkAct TLS_HANDSHAKE TLS_SERVER_KEY 'S'
  else if m == SERVER_CERT_REQ then mkAct TLS_HANDSHAKE TLS_SERVER_CERT_REQ 'S'
  else if m == SERVER_HELLO_DONE then mkAct TLS_HANDSHAKE TLS_SERVER_HELLO_DONE 'S'
  else if m == CLIENT_CERT then mkAct TLS_HANDSHAKE TLS_CLIENT_CERT 'C'
  else if m == CLIENT_KEY then mkAct TLS_HANDSHAKE TLS_CLIENT_KEY 'C'
  else if m == CLIENT_CERT_VERIFY then mkAct TLS_HANDSHAKE TLS_CLIENT_CERT_VERIFY 'C'
  else if m == CLIENT_CHANGE_CIPHER_SPEC then mkAct TLS_CHANGE_CIPHER_SPEC 0 'C'
  else if m == CLIENT_FINISHED then mkAct TLS_HANDSHAKE TLS_CLIENT_FINISHED 'C'
  else if m == SERVER_CHANGE_CIPHER_SPEC then mkAct TLS_CHANGE_CIPHER_SPEC 0 'S'
  else if m == SERVER_FINISHED then mkAct TLS_HANDSHAKE TLS_SERVER_FINISHED 'S'
  else if m == APPLICATION_DATA then mkAct TLS_APPLICATION_DATA 0 'B'
  else zero

// A model of the handshake state machine (array handshakes in C)
handshakes : [128][16][32]
handshakes = [handshakes_fn h | h <- [0..127]]

// A function that gives the handshake sequence for each valid handshake_type.
// This is a sparse encoding of the handshakes array (which is sparse too).
handshakes_fn : [32] -> [16][32]
handshakes_fn handshk =
  if handshk == INITIAL then [CLIENT_HELLO, SERVER_HELLO] # zero else
  if handshk == (NEGOTIATED) 
    then  [ CLIENT_HELLO, SERVER_HELLO, SERVER_CHANGE_CIPHER_SPEC 
          , SERVER_FINISHED, CLIENT_CHANGE_CIPHER_SPEC, CLIENT_FINISHED
          , APPLICATION_DATA
          ] # zero else
  if handshk == (NEGOTIATED || WITH_SESSION_TICKET) 
    then [ CLIENT_HELLO, SERVER_HELLO, SERVER_NEW_SESSION_TICKET
         , SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED
         , CLIENT_CHANGE_CIPHER_SPEC, CLIENT_FINISHED
         , APPLICATION_DATA
         ] # zero else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE) 
    then [ CLIENT_HELLO, SERVER_HELLO, SERVER_CERT, SERVER_HELLO_DONE
         , CLIENT_KEY, CLIENT_CHANGE_CIPHER_SPEC, CLIENT_FINISHED
         , SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED, APPLICATION_DATA
         ] # zero else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || WITH_SESSION_TICKET) 
    then [ CLIENT_HELLO, SERVER_HELLO, SERVER_CERT, SERVER_HELLO_DONE
         , CLIENT_KEY, CLIENT_CHANGE_CIPHER_SPEC, CLIENT_FINISHED
         , SERVER_NEW_SESSION_TICKET, SERVER_CHANGE_CIPHER_SPEC
         , SERVER_FINISHED, APPLICATION_DATA] # zero else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || PERFECT_FORWARD_SECRECY)
    then [ CLIENT_HELLO, SERVER_HELLO, SERVER_CERT, SERVER_KEY
         , SERVER_HELLO_DONE, CLIENT_KEY, CLIENT_CHANGE_CIPHER_SPEC
         , CLIENT_FINISHED, SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED
         , APPLICATION_DATA
         ] # zero else
  if handshk == ( NEGOTIATED || FULL_HANDSHAKE 
                || PERFECT_FORWARD_SECRECY || WITH_SESSION_TICKET)
    then [ CLIENT_HELLO, SERVER_HELLO, SERVER_CERT, SERVER_KEY
         , SERVER_HELLO_DONE, CLIENT_KEY, CLIENT_CHANGE_CIPHER_SPEC
         , CLIENT_FINISHED, SERVER_NEW_SESSION_TICKET
         , SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED, APPLICATION_DATA
         ] # zero else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || OCSP_STATUS) 
    then [ CLIENT_HELLO, SERVER_HELLO, SERVER_CERT, SERVER_CERT_STATUS
         , SERVER_HELLO_DONE, CLIENT_KEY, CLIENT_CHANGE_CIPHER_SPEC
         , CLIENT_FINISHED, SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED
         , APPLICATION_DATA
         ] # zero else
  if handshk == (  NEGOTIATED || OCSP_STATUS || FULL_HANDSHAKE 
                || WITH_SESSION_TICKET)
    then [ CLIENT_HELLO, SERVER_HELLO, SERVER_CERT, SERVER_CERT_STATUS
         , SERVER_HELLO_DONE, CLIENT_KEY, CLIENT_CHANGE_CIPHER_SPEC
         , CLIENT_FINISHED, SERVER_NEW_SESSION_TICKET
         , SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED, APPLICATION_DATA
         ] # zero else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || PERFECT_FORWARD_SECRECY || OCSP_STATUS) then [CLIENT_HELLO, SERVER_HELLO, SERVER_CERT, SERVER_CERT_STATUS,
    SERVER_KEY, SERVER_HELLO_DONE, CLIENT_KEY, CLIENT_CHANGE_CIPHER_SPEC,
    CLIENT_FINISHED, SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED, APPLICATION_DATA] # zero else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || PERFECT_FORWARD_SECRECY || OCSP_STATUS || WITH_SESSION_TICKET) then [CLIENT_HELLO, SERVER_HELLO, SERVER_CERT, SERVER_CERT_STATUS,
    SERVER_KEY, SERVER_HELLO_DONE, CLIENT_KEY, CLIENT_CHANGE_CIPHER_SPEC, CLIENT_FINISHED, 
    SERVER_NEW_SESSION_TICKET, SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED, APPLICATION_DATA] # zero else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || CLIENT_AUTH)
    then [CLIENT_HELLO, SERVER_HELLO, SERVER_CERT, SERVER_CERT_REQ, SERVER_HELLO_DONE, CLIENT_CERT, CLIENT_KEY, CLIENT_CERT_VERIFY, CLIENT_CHANGE_CIPHER_SPEC, CLIENT_FINISHED,
            SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED,
            APPLICATION_DATA] # zero
  else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || CLIENT_AUTH || WITH_SESSION_TICKET)
    then [CLIENT_HELLO, SERVER_HELLO, SERVER_CERT, SERVER_CERT_REQ, SERVER_HELLO_DONE, CLIENT_CERT, CLIENT_KEY, CLIENT_CERT_VERIFY, CLIENT_CHANGE_CIPHER_SPEC, CLIENT_FINISHED,
            SERVER_NEW_SESSION_TICKET, SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED,
            APPLICATION_DATA] # zero
  else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || CLIENT_AUTH || NO_CLIENT_CERT)
  then [CLIENT_HELLO,
            SERVER_HELLO, SERVER_CERT, SERVER_CERT_REQ, SERVER_HELLO_DONE,
            CLIENT_CERT, CLIENT_KEY, CLIENT_CHANGE_CIPHER_SPEC, CLIENT_FINISHED,
            SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED,
            APPLICATION_DATA] # zero
  else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || CLIENT_AUTH || NO_CLIENT_CERT || WITH_SESSION_TICKET)
  then [CLIENT_HELLO,
            SERVER_HELLO, SERVER_CERT, SERVER_CERT_REQ, SERVER_HELLO_DONE,
            CLIENT_CERT, CLIENT_KEY, CLIENT_CHANGE_CIPHER_SPEC, CLIENT_FINISHED,
            SERVER_NEW_SESSION_TICKET, SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED,
            APPLICATION_DATA] # zero
  else
  if handshk ==  (NEGOTIATED || FULL_HANDSHAKE || PERFECT_FORWARD_SECRECY || CLIENT_AUTH) then [            CLIENT_HELLO,
          SERVER_HELLO, SERVER_CERT, SERVER_KEY, SERVER_CERT_REQ, SERVER_HELLO_DONE,
          CLIENT_CERT, CLIENT_KEY, CLIENT_CERT_VERIFY, CLIENT_CHANGE_CIPHER_SPEC, CLIENT_FINISHED,
          SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED,
          APPLICATION_DATA] # zero
  else
  if handshk ==  (NEGOTIATED || FULL_HANDSHAKE || PERFECT_FORWARD_SECRECY || CLIENT_AUTH || WITH_SESSION_TICKET) then [            CLIENT_HELLO,
          SERVER_HELLO, SERVER_CERT, SERVER_KEY, SERVER_CERT_REQ, SERVER_HELLO_DONE,
          CLIENT_CERT, CLIENT_KEY, CLIENT_CERT_VERIFY, CLIENT_CHANGE_CIPHER_SPEC, CLIENT_FINISHED,
          SERVER_NEW_SESSION_TICKET, SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED,
          APPLICATION_DATA] # zero
  else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || PERFECT_FORWARD_SECRECY || CLIENT_AUTH || NO_CLIENT_CERT)
  then [CLIENT_HELLO,
          SERVER_HELLO, SERVER_CERT, SERVER_KEY, SERVER_CERT_REQ, SERVER_HELLO_DONE,
          CLIENT_CERT, CLIENT_KEY, CLIENT_CHANGE_CIPHER_SPEC, CLIENT_FINISHED,
          SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED,
          APPLICATION_DATA] # zero
  else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || PERFECT_FORWARD_SECRECY || CLIENT_AUTH || NO_CLIENT_CERT || WITH_SESSION_TICKET)
  then [CLIENT_HELLO,
          SERVER_HELLO, SERVER_CERT, SERVER_KEY, SERVER_CERT_REQ, SERVER_HELLO_DONE,
          CLIENT_CERT, CLIENT_KEY, CLIENT_CHANGE_CIPHER_SPEC, CLIENT_FINISHED,
          SERVER_NEW_SESSION_TICKET, SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED,
          APPLICATION_DATA] # zero
  else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || OCSP_STATUS || CLIENT_AUTH) then [
            CLIENT_HELLO, SERVER_HELLO, SERVER_CERT, SERVER_CERT_STATUS, SERVER_CERT_REQ, SERVER_HELLO_DONE, CLIENT_CERT, CLIENT_KEY, CLIENT_CERT_VERIFY, CLIENT_CHANGE_CIPHER_SPEC, CLIENT_FINISHED, SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED, APPLICATION_DATA
        ] # zero
  else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || OCSP_STATUS || CLIENT_AUTH || WITH_SESSION_TICKET) then [
            CLIENT_HELLO, SERVER_HELLO, SERVER_CERT, SERVER_CERT_STATUS, SERVER_CERT_REQ, SERVER_HELLO_DONE, CLIENT_CERT, CLIENT_KEY, CLIENT_CERT_VERIFY, CLIENT_CHANGE_CIPHER_SPEC, CLIENT_FINISHED, SERVER_NEW_SESSION_TICKET, SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED, APPLICATION_DATA
        ] # zero
  else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || OCSP_STATUS || CLIENT_AUTH || NO_CLIENT_CERT)
    then [  CLIENT_HELLO, SERVER_HELLO, SERVER_CERT
         , SERVER_CERT_STATUS, SERVER_CERT_REQ, SERVER_HELLO_DONE
         , CLIENT_CERT, CLIENT_KEY, CLIENT_CHANGE_CIPHER_SPEC
         , CLIENT_FINISHED, SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED
         , APPLICATION_DATA
         ] # zero
  else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || OCSP_STATUS || CLIENT_AUTH || NO_CLIENT_CERT || WITH_SESSION_TICKET)
    then [  CLIENT_HELLO, SERVER_HELLO, SERVER_CERT
         , SERVER_CERT_STATUS, SERVER_CERT_REQ, SERVER_HELLO_DONE
         , CLIENT_CERT, CLIENT_KEY, CLIENT_CHANGE_CIPHER_SPEC
         , CLIENT_FINISHED, SERVER_NEW_SESSION_TICKET, SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED
         , APPLICATION_DATA
         ] # zero
  else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || PERFECT_FORWARD_SECRECY || OCSP_STATUS || CLIENT_AUTH) then
  [CLIENT_HELLO,
            SERVER_HELLO, SERVER_CERT, SERVER_CERT_STATUS, SERVER_KEY, SERVER_CERT_REQ, SERVER_HELLO_DONE,
            CLIENT_CERT, CLIENT_KEY, CLIENT_CERT_VERIFY, CLIENT_CHANGE_CIPHER_SPEC, CLIENT_FINISHED,
            SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED,
            APPLICATION_DATA] # zero
  else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || PERFECT_FORWARD_SECRECY || OCSP_STATUS || CLIENT_AUTH || WITH_SESSION_TICKET) then
  [CLIENT_HELLO,
            SERVER_HELLO, SERVER_CERT, SERVER_CERT_STATUS, SERVER_KEY, SERVER_CERT_REQ, SERVER_HELLO_DONE,
            CLIENT_CERT, CLIENT_KEY, CLIENT_CERT_VERIFY, CLIENT_CHANGE_CIPHER_SPEC, CLIENT_FINISHED,
            SERVER_NEW_SESSION_TICKET, SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED,
            APPLICATION_DATA] # zero
  else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || PERFECT_FORWARD_SECRECY || OCSP_STATUS || CLIENT_AUTH || NO_CLIENT_CERT)
    then [ CLIENT_HELLO, SERVER_HELLO, SERVER_CERT, SERVER_CERT_STATUS
         , SERVER_KEY, SERVER_CERT_REQ, SERVER_HELLO_DONE
         , CLIENT_CERT, CLIENT_KEY, CLIENT_CHANGE_CIPHER_SPEC
         , CLIENT_FINISHED, SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED
         , APPLICATION_DATA 
         ] # zero
  else
  if handshk == (NEGOTIATED || FULL_HANDSHAKE || PERFECT_FORWARD_SECRECY || OCSP_STATUS || CLIENT_AUTH || NO_CLIENT_CERT || WITH_SESSION_TICKET)
    then [ CLIENT_HELLO, SERVER_HELLO, SERVER_CERT, SERVER_CERT_STATUS
         , SERVER_KEY, SERVER_CERT_REQ, SERVER_HELLO_DONE
         , CLIENT_CERT, CLIENT_KEY, CLIENT_CHANGE_CIPHER_SPEC
         , CLIENT_FINISHED, SERVER_NEW_SESSION_TICKET, SERVER_CHANGE_CIPHER_SPEC, SERVER_FINISHED
         , APPLICATION_DATA
         ] # zero
  else zero

// A predicate to tell whether a given handshake struct is valid
// (i.e. the fields are indexing into a valid state of the handshake
// state machine)
valid_handshake : handshake -> Bit
valid_handshake hs = (handshakes_fn hs.handshake_type) != zero /\
                     (hs.message_number < length handshakefn - 1) /\
                     ((hs.message_number > 0) ==> (handshakefn@(hs.message_number + 1) != 0)) where
                     handshakefn = (handshakes_fn hs.handshake_type)


valid_connection : connection -> Bit
valid_connection conn = valid_handshake (conn.handshake)

// Tells if the connection struct is in a valid initial state
initial_connection : connection -> Bit
initial_connection conn = conn.handshake.handshake_type == INITIAL /\ conn.handshake.message_number == 0

// Logical implication
implies : Bit -> Bit -> Bit
implies l r = if r then True else ~l

// values of similarly named C macros and enum values
INITIAL : [32]
INITIAL =  zero # 0x00 
NEGOTIATED : [32]
NEGOTIATED = zero #  0x01
RESUME : [32]
RESUME = zero # 0x01
FULL_HANDSHAKE : [32]
FULL_HANDSHAKE = zero # 0x02
PERFECT_FORWARD_SECRECY : [32]
PERFECT_FORWARD_SECRECY= zero # 0x04
OCSP_STATUS : [32]
OCSP_STATUS = zero # 0x08
WITH_SESSION_TICKET : [32]
WITH_SESSION_TICKET = zero # 0x20
CLIENT_AUTH : [32]
CLIENT_AUTH = zero # 0x10
NO_CLIENT_CERT : [32]
NO_CLIENT_CERT = zero # 0x40


CLIENT_HELLO = 0
SERVER_HELLO = 1
SERVER_CERT = 2
SERVER_NEW_SESSION_TICKET = 3
SERVER_CERT_STATUS = 4
SERVER_KEY = 5
SERVER_CERT_REQ = 6
SERVER_HELLO_DONE = 7
CLIENT_CERT = 8
CLIENT_KEY = 9
CLIENT_CERT_VERIFY = 10
CLIENT_CHANGE_CIPHER_SPEC = 11
CLIENT_FINISHED = 12
SERVER_CHANGE_CIPHER_SPEC = 13
SERVER_FINISHED = 14
APPLICATION_DATA = 15

//TLS record type
TLS_CHANGE_CIPHER_SPEC = 20
TLS_ALERT = 21
TLS_HANDSHAKE = 22
TLS_APPLICATION_DATA = 23

//TLS handshake message types
TLS_HELLO_REQUEST = 0
TLS_CLIENT_HELLO  = 1
TLS_SERVER_HELLO  = 2
TLS_SERVER_NEW_SESSION_TICKET = 4
TLS_SERVER_CERT               = 11
TLS_SERVER_KEY                = 12
TLS_SERVER_CERT_REQ           = 13
TLS_SERVER_HELLO_DONE         = 14
TLS_CLIENT_CERT               = 11
TLS_CLIENT_CERT_VERIFY        = 15
TLS_CLIENT_KEY                = 16
TLS_CLIENT_FINISHED           = 20
TLS_SERVER_FINISHED           = 20  /* Same as CLIENT_FINISHED */
TLS_SERVER_CERT_STATUS        = 22

S2N_SERVER = 0
S2N_CLIENT = 1
